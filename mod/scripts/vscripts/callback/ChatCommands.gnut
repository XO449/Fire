global function ChatCommands_Init
global function AddChatCommandCallback
global function SetChatCommandCallback
global function IsChatCommandRegistered
global function RemoveChatCommandCallback

/*
global struct ChatCommand
{
    string command
    void functionref(entity, array<string>) callback
}
*/

table< string, void functionref(entity player, array<string> args) > ChatCommandCallbacks = {}

void function ChatCommands_Init()
{
    AddCallback_OnReceivedSayTextMessage( OnReceivedSayTextMessage )
}

#if SERVER
ClServer_MessageStruct function OnReceivedSayTextMessage(ClServer_MessageStruct message)
#else
ClClient_MessageStruct function OnReceivedSayTextMessage(ClClient_MessageStruct message)
#endif
{
    entity player = message.player
    string chatText = message.message

    array<string> words = split(chatText, " ")
    string command = words[0]
    array<string> args = []
    for(int i = 1; i < words.len(); i++) {
        if(words[i] != "")
            args.append(words[i])
    }
    
    if(command in ChatCommandCallbacks)
    {
        array<string> args = words.slice(1)
        thread ChatCommandCallbacks[command]( player, args )
        #if SERVER
        message.shouldBlock = true
        #endif
    }

    return message
}

bool function AddChatCommandCallback(string command, void functionref(entity player, array<string> args) func)
{
    if( command == "" || func == null )
        return false
    if(command in ChatCommandCallbacks)
        return false
    ChatCommandCallbacks[command] <- func
    return true
}

bool function SetChatCommandCallback(string command, void functionref(entity player, array<string> args) func)
{
    if( command == "" || !(command in ChatCommandCallbacks) )
        return false
    ChatCommandCallbacks[command] = func
    return true
}

bool function IsChatCommandRegistered(string command)
{
    if( command == "" )
        return false
    return (command in ChatCommandCallbacks)
}

bool function RemoveChatCommandCallback(string command)
{
    if( command == "" || !(command in ChatCommandCallbacks) )
        return false
    delete ChatCommandCallbacks[command]
    return true
}